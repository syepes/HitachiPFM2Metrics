apply plugin:'groovy'
apply plugin:'distribution'

def projectName = 'HitachiPFM2Metrics'
description = "$projectName - Hitachi Performance Management (PFM) Integration with Graphite"

group = 'com.allthingsmonitoring.hitachi.HitachiPFM2Metrics'
archivesBaseName = 'HitachiPFM2Metrics'
distsDirName = 'dist'
version = '1.2'

def authorString = 'Sebastian YEPES FERNANDEZ (syepes@gmail.com)'
def copyrightString = 'Copyright &#169; 2015 &#8211; Sebastian YEPES FERNANDEZ All Rights Reserved.'


defaultTasks 'clean','build','groovydoc','distZip'


configurations {
  groovyDoc
}

// http://svn.codehaus.org/griffon/builders/jidebuilder/trunk/build.gradle
// http://gparallelizer.googlecode.com/svn/trunk/build.gradle
configure([compileGroovy, compileTestGroovy]) {
  //groovyOptions.useAnt = true
  //compileGroovy.groovyOptions.fork = true
  groovyOptions.fork(memoryInitialSize: '128M', memoryMaximumSize: '1G')
  groovyOptions.encoding = 'UTF-8'
  //groovyOptions.optimizationOptions.indy = true
  sourceCompatibility = 1.7
  targetCompatibility = 1.7
}

//configurations.compile.transitive = false
compileJava {
  options.encoding = 'UTF-8'
  sourceCompatibility = 1.7
  targetCompatibility = 1.7
}


repositories {
  mavenLocal()
  maven { url 'http://oss.sonatype.org/content/groups/public/' }
  mavenCentral()
}

dependencies {
  compile 'org.codehaus.groovy:groovy:2.+',
          'org.codehaus.groovy:groovy-nio:2.+',
          'org.slf4j:slf4j-api:1.7.+',
          'ch.qos.logback:logback-core:1.0.+',
          'ch.qos.logback:logback-classic:1.0.+',
          fileTree(dir: 'libs', include: '*.jar')

  compile 'org.codehaus.jsr166-mirror:jsr166y:1.7.+'
  compile 'org.codehaus.gpars:gpars:1.+', {
            exclude module: "groovy-all"
          }

  groovyDoc 'org.codehaus.groovy:groovy-groovydoc:2.+',
            'org.codehaus.groovy:groovy-ant:2.+'

  testCompile "org.spockframework:spock-core:1.0-groovy-2.4", {
    exclude module: "groovy-all"
  }
}


groovydoc {
  groovyClasspath = configurations.groovyDoc
  destinationDir  = new File(( System.getProperty( 'groovydocDir' ) ?: project.buildDir.path + '/docs' ), ( project.version.contains( '-SNAPSHOT' ) ? '' : project.version ))
  includePrivate = true

  windowTitle = "$projectName"
  docTitle = "$projectName - v$project.version"
  header = ""
  footer = copyrightString
  overview ""

  link 'http://static.springsource.org/spring/docs/3.1.x/javadoc-api/', 'org.springframework.'
  link 'http://download.oracle.com/javase/6/docs/api/',                 'java.'
  link 'http://www.groovy-lang.org/api/',                               'groovy.', 'org.codehaus.groovy.'
  link 'http://commons.apache.org/net/apidocs/',                        'org.apache.commons.net.'
}


jar {
  from ( configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } ) {
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
  }
  manifest {
    attributes 'Implementation-Title': project.description,
               'Implementation-Version': project.version,
               'Specification-Title': project.description,
               'Specification-Version': project.version,
               'Built-By': System.properties['user.name'],
               'Built-JDK': System.properties['java.version'] +' ('+ System.properties['java.vm.vendor'] +')',
               'Built-Date': new Date(),
               'Author': authorString,
               'Main-Class': project.group
  }
}

distributions {
  main {
    baseName = archivesBaseName
    contents {
      from { 'build/libs' }
      from { 'src/dist' }
    }
  }
}
distZip.dependsOn('jar')
distTar.dependsOn('jar')
distTar.compression = Compression.GZIP

